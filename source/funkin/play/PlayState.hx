package funkin.play;

import flixel.FlxCamera;
import flixel.FlxObject;
import flixel.FlxSubState;
import flixel.math.FlxMath;
import flixel.math.FlxPoint;
import flixel.tweens.FlxTween;
import funkin.audio.FunkinSound;
import funkin.data.song.SongData.SongNoteData;
import funkin.data.stage.StageRegistry;
import funkin.graphics.FunkinBar;
import funkin.graphics.FunkinText;
import funkin.play.note.HoldNoteSprite;
import funkin.play.note.NoteDirection;
import funkin.play.note.NoteSprite;
import funkin.play.note.Strumline;
import funkin.play.popup.Popups;
import funkin.play.song.Song;
import funkin.play.song.Voices;
import funkin.ui.FunkinState;
import funkin.util.MathUtil;
import funkin.util.RhythmUtil;

/**
 * A state where the gameplay occurs. Kinda like a "play" state. Hah! I said the thing!
 */
class PlayState extends FunkinState
{
	public static var instance:PlayState;
	public static var difficulty:String;
	public static var song:Song;

	public var deaths:Int = 0;
	
	public var camFollow:FlxObject;
	public var stage:Stage;

	var songLoaded:Bool;
	var songStarted:Bool;
	var songEnded:Bool;

	var score:Float;
	var health:Float;
	var healthLerp:Float;

	var camGame:FlxCamera;
	var camHUD:FlxCamera;
	var camPause:FlxCamera;

	var camTarget:Int;
	var voices:Voices;

	var opponentStrumline:Strumline;
	var playerStrumline:Strumline;
	var healthBar:FunkinBar;
	var scoreText:FunkinText;
	var countdown:Countdown;
	var popups:Popups;

	override public function create()
	{
		instance = this;

		camGame = new FlxCamera();
		FlxG.cameras.reset(camGame);

		camHUD = new FlxCamera();
		camHUD.bgColor = 0x0;
		FlxG.cameras.add(camHUD, false);

		camPause = new FlxCamera();
		camPause.bgColor = 0x0;
		FlxG.cameras.add(camPause, false);

		camFollow = new FlxObject();
		camFollow.active = false;

		camGame.follow(camFollow, LOCKON, 0.03);

		healthBar = new FunkinBar(0, 0, 500, 15, 0, 2, true);
		healthBar.screenCenter(X);
		healthBar.y = FlxG.height - healthBar.height - 60;
		healthBar.camera = camHUD;
		add(healthBar);

		scoreText = new FunkinText(0, 0, '123456');
		scoreText.y = FlxG.height - scoreText.height - 20;
		scoreText.alignment = CENTER;
		scoreText.camera = camHUD;
		scoreText.size = 20;
		add(scoreText);

		if (Preferences.downscroll)
		{
			healthBar.y = 60;
			scoreText.y = 20;
		}

		opponentStrumline = new Strumline();
		opponentStrumline.x = 325;
		opponentStrumline.camera = camHUD;
		opponentStrumline.noteHit.add(opponentNoteHit);
		opponentStrumline.holdNoteHit.add(opponentHoldNoteHit);
		add(opponentStrumline);

		playerStrumline = new Strumline();
		playerStrumline.x = FlxG.width - opponentStrumline.x;
		playerStrumline.camera = camHUD;
		playerStrumline.noteHit.add(playerNoteHit);
		playerStrumline.noteMiss.add(playerNoteMiss);
		playerStrumline.holdNoteHit.add(playerHoldNoteHit);
		playerStrumline.holdNoteDrop.add(playerHoldNoteDrop);
		add(playerStrumline);

		stage = StageRegistry.instance.fetchStage(song.stage);
		add(stage);

		countdown = new Countdown();
		countdown.camera = camHUD;
		add(countdown);

		popups = new Popups();
		add(popups);

		loadSong();
		loadCharacters();

		resetCameraTarget();
		resetSong();

		super.create();
	}

	override public function update(elapsed:Float)
	{
		super.update(elapsed);

		// Updates the song and input
		if (songLoaded)
		{
			conductor.time += elapsed * Constants.MS_PER_SEC;
			conductor.update();

			if (conductor.time >= 0 && !songStarted) startSong();

			checkSongTime();
		}

		opponentStrumline.process(false);
		playerStrumline.process(!Preferences.botplay);

		processInput();

		// HUD
		health = FlxMath.bound(health, healthBar.min, healthBar.max);
		healthLerp = MathUtil.lerp(healthLerp, health, 0.15);
		healthBar.value = healthLerp;

		scoreText.text = Std.string(Std.int(score));
		scoreText.screenCenter(X);
		
		camGame.zoom = MathUtil.lerp(camGame.zoom, stage.zoom, 0.03);
		camHUD.zoom = MathUtil.lerp(camHUD.zoom, 1, 0.03);

		if (controls.PAUSE)
			pauseGame();
		if (controls.RESET)
			health = 0;

		// Death :(
		if (health <= healthBar.min) openSubState(new GameOverSubState());
	}

	override function beatHit(beat:Int)
	{
		super.beatHit(beat);

		if (subState != null) return;

		// Camera bopping
		if (beat % 2 == 0)
		{
			camGame.zoom = stage.zoom + 0.05;
			camHUD.zoom = 1.02;
		}

		// Character bopping
		stage.opponent?.dance();
		stage.player?.dance();
		stage.gf?.dance();

		// Advances the countdown by a step
		countdown.advance();
	}

	override function sectionHit(section:Int)
	{
		super.sectionHit(section);
		
		if (!songStarted || subState != null) return;

		// Moves the camera
		// TODO: Remove this once events are added
		setCameraTarget(1 - camTarget);
	}

	public function resetSong()
	{
		songStarted = false;
		songEnded = false;

		score = 0;
		health = Constants.DEFAULT_HEALTH;
		healthLerp = health;
		
		camGame.zoom = stage.zoom;

		// Loads the strumline
		var notes:Array<SongNoteData> = song.getNotes(difficulty);
		var speed:Float = song.getSpeed(difficulty);

		opponentStrumline.clean();
		playerStrumline.clean();

		opponentStrumline.load(notes.filter(note -> return note.d >= Constants.NOTE_COUNT), speed);
		playerStrumline.load(notes.filter(note -> return note.d < Constants.NOTE_COUNT), speed);

		// Resets the conductor
		conductor.reset(song.bpm);
		conductor.time = -conductor.crotchet * 4;

		// Starts the countdown
		countdown.start();

		FunkinSound.stopAllSounds(true);

		resetCameraTarget();
	}

	public function setCameraTarget(target:Int, instant:Bool = false)
	{
		camTarget = target;

		var character:Character = stage.opponent;
		if (target == 1) character = stage.player;

		if (character == null) return;

		var camPos:FlxPoint = character.getGraphicMidpoint();
		camPos.x += character.isPlayer ? -100 : 100;
		camPos.y -= 100;
		
		camFollow.setPosition(camPos.x, camPos.y);

		if (instant) camGame.snapToTarget();
	}

	function loadCharacters()
	{
		stage.setPlayer(song.player);
		stage.setOpponent(song.opponent);
		stage.setGF(song.gf);
	}

	function loadSong()
	{
		songLoaded = true;

		FunkinSound.playMusic(Paths.inst(song.id), 1, false, false);
		voices = new Voices(song.id);
	}

	function startSong()
	{
		songStarted = true;

		FunkinSound.music.play();
		voices.play();
	}

	function endSong()
	{
		songEnded = true;

		FunkinSound.stopAllSounds(true);

		// TODO: Add song end logic
	}

	function checkSongTime()
	{
		// End the song if the time has come...
		// Doing this normally has a problem unfortunately :(
		if (conductor.time >= FunkinSound.music.length)
		{
			endSong();
			return;
		}

		// Don't resync if the song isn't playing
		if (!FunkinSound.music.playing) return;

		if (Math.abs(conductor.time - FunkinSound.music.time) > Constants.RESYNC_THRESHOLD)
		{
			FunkinSound.music.pause();
			FunkinSound.music.time = conductor.time;
			FunkinSound.music.play();

			trace('Resynced instrumental.');
		}

		voices.checkResync(conductor.time);
	}

	function pauseGame()
	{
		var pause:PauseSubState = new PauseSubState();
		pause.camera = camPause;
		openSubState(pause);
	}

	function resetCameraTarget()
	{
		// Target the opponent if there is one
		// If not, try and target the player
		if (stage.opponent != null)
			setCameraTarget(0, true);
		else if (stage.player != null)
			setCameraTarget(1, true);
	}

	function processInput()
	{
		// Player input
		var directionNotes:Array<Array<NoteSprite>> = [[], [], [], []];

		for (note in playerStrumline.getMayHitNotes()) directionNotes[note.direction].push(note);

		for (i in 0...directionNotes.length)
		{
			var note:NoteSprite = directionNotes[i][0];
			var direction:NoteDirection = NoteDirection.fromInt(i);
			var pressed:Bool = direction.justPressed || Preferences.botplay;

			// Miss if ghost tapping is disabled
			// Don't count the miss if botplay is enabled though
			if (note == null && pressed && !Preferences.ghostTapping && !Preferences.botplay) playerGhostMiss(direction);

			// Don't hit the note if nothing's being pressed
			// Especially don't hit the note if it's null
			if (!pressed || note == null) continue;

			playerStrumline.hitNote(note);
		}

		// Opponent input
		for (note in opponentStrumline.getMayHitNotes())
			opponentStrumline.hitNote(note);
	}

	function playerNoteHit(note:NoteSprite)
	{
		var judgement:Judgement = RhythmUtil.judgeNote(note);
		popups.playJudgement(judgement);

		// Only play the note splash if the player got a Sick!
		if (judgement == SICK) playerStrumline.playSplash(note.direction);

		score += judgement.score;
		health += Constants.NOTE_HEALTH;

		stage.player?.sing(note.direction);
		voices.playerVolume = 1;
	}

	function playerHoldNoteHit(holdNote:HoldNoteSprite)
	{
		score += Constants.HOLD_SCORE_PER_SEC * FlxG.elapsed;
		health += Constants.HOLD_HEALTH_PER_SEC * FlxG.elapsed;

		stage.player?.resetSingTimer();
		voices.playerVolume = 1;
	}

	function playerNoteMiss(note:NoteSprite)
	{
		var missScore:Float = Constants.MISS_SCORE;
		if (note.holdNote != null) missScore *= (note.holdNote.length / 500);

		score += missScore;
		health += Constants.MISS_HEALTH;

		stage.player?.miss(note.direction);
		voices.playerVolume = 0;
	}

	function playerGhostMiss(direction:NoteDirection)
	{
		score += Constants.GHOST_MISS_SCORE;
		health += Constants.GHOST_MISS_HEALTH;

		stage.player?.miss(direction);
		voices.playerVolume = 0;
	}

	function playerHoldNoteDrop(holdNote:HoldNoteSprite)
	{
		// Takes away score based on how long the hold note is
		score += Constants.MISS_SCORE * (holdNote.length / 500);
		health += Constants.MISS_HEALTH;

		stage.player?.miss(holdNote.direction);
		voices.playerVolume = 0;
	}

	function opponentNoteHit(note:NoteSprite)
	{
		stage.opponent?.sing(note.direction);
	}

	function opponentHoldNoteHit(holdNote:HoldNoteSprite)
	{
		stage.opponent?.resetSingTimer();
	}

	override public function openSubState(subState:FlxSubState)
	{
		super.openSubState(subState);

		FunkinSound.music.pause();
		voices.pause();

		FlxTween.globalManager.active = false;
		FlxG.sound.defaultSoundGroup.pause();

		camGame.active = false;
	}

	override public function closeSubState()
	{
		super.closeSubState();

		if (songStarted && !songEnded)
		{
			FunkinSound.music.play();
			voices.play();
		}

		FlxTween.globalManager.active = true;
		FlxG.sound.defaultSoundGroup.resume();

		camGame.active = true;
	}

	override public function destroy()
	{
		super.destroy();

		FunkinSound.music.destroy();

		// Gonna want to reactivate this when the state is destroyed
		// There are problems if this isn't done
		FlxTween.globalManager.active = true;
	}
}
